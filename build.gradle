plugins {
    id 'java'
    id 'org.springframework.boot' version '2.0.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.5.RELEASE'
    id 'io.franzbecker.gradle-lombok' version '1.14'
}

lombok {
    version = '1.18.2'
    sha256 = ''
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-webflux')
    implementation('org.springframework.boot:spring-boot-devtools')

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude module: 'junit'
    }
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.3.1')
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.3.1')
    testImplementation('org.junit.platform:junit-platform-launcher:1.3.1')
    // FIXME: This breaks creation of fat jar...
    testImplementation('io.github.bonigarcia:selenium-jupiter:2.2.0')

    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.3.1')
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

wrapper {
    gradleVersion = '4.10.2'
}

task testJar(type: Jar) {
    description = 'Create runnable jar with testImplementation dependencies'
    classifier = 'test'

    manifest {
        attributes 'Main-Class': 'com.sandbox.TestRunner'
    }

    from {
        configurations.testRuntimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from sourceSets.test.output

    with jar
}
